#+OPTIONS: ^:{}
* Practical guide to use Gnus with Gmail                                        :emacs:gnus:email:gmail:
  :PROPERTIES:
  :ID:       o2b:C45FB98A-3872-4877-9E50-5BBAFAE0561C
  :POST_DATE: 2012-10-12 17:14:00
  :POST_SLUG: notes-on-using-gnus
  :POSTID:   403
  :UPDATE_DATE: 2014-08-09 06:18:51
  :END:
Author: Chen Bin (redguardtoo)

Version: 20151110

Updated: <2015-12-13 Sun>

Created: <2012-10-12 Mon>

Copyright: This work is licensed under the [[http://creativecommons.org/licenses/by-nc-nd/3.0/][Creative Commons Attribution-NonCommercial-NoDerivs 3.0 Unported License]].

** Introduction
Gnus is a nice email client.

But many newbies get lost in its features. It would be much easier if they learned the *essential 5% features* and ignore the remaining 95% at the beginning.

So here is my guide on the 5%.

** Why (OPTIONAL)
- Gnus is *solid*
- Works on all platforms
- Less hardware resource required
- Mail search and filter is great
- Bundled with Emacs, no extra installation needed
- Usable when offline. Check the "offline" part in the section "Advanced tips"
- powerful combined with other plugins like [[https://github.com/capitaomorte/yasnippet][yasnippet]]

** Quick start
*** Requirements and conventions
A little homework is needed before we start,
- This guide applies on Emacs24+
- You need install GnuTLS and OpenSSH on Linux/Mac/Window(Cygwin)
- `C` means `Ctrl` and `M` means `Alt`. For example, `M-x` means pressing `Alt` and `X` together.
- `M-x mycommand` means pressing `Alt` and `X`, input "mycommand", and press `Enter` key.
- `RET` means `Enter` key.
- "Group" means "mail folder".
- "Group buffer" means the list of folders.
- "Summary buffer" means the list of mails.

*** Sample setup
Now please check [[http://www.emacswiki.org/emacs/GnusGmail]] for basics. You can also use my settings at the end of this article.

*** Usage
After setup, `M-x gnus` in Emacs.

You will come to the "Group Buffer" window. The "Group Buffer" lists the mail folders. In Gnus, the folder is called "Group". By default, the folders are *invisible*!

Please subscribe the groups to make it visible. For example, INBOX is named "nnimap+gmail:INBOX" in "Group Buffer" and *it's invisible at the beginning*!. I admit it's confusing.

Check section "Subscribe groups" for details.

At minimum, you need subcribe INBOX group. But after subscribing the INBOX is still *invisible* if INBOX contains NO unread mails. Yes, I agree with you this makes no sense for a modern email client!

You can `C-u 5 M-x gnus-group-list-all-groups` to solve this problem.

You can insert below code into ~/.emacs to create hotkey `o` for `C-u 5 M-x gnus-group-list-all-groups` :
#+BEGIN_SRC emacs-lisp
(defun my-gnus-group-list-subscribed-groups ()
  "List all subscribed groups with or without un-read messages"
  (interactive)
  (gnus-group-list-all-groups 5))

(define-key gnus-group-mode-map 
  ;; list all the subscribed groups even they contain zero un-read messages
  (kbd "o") 'my-gnus-group-list-subscribed-groups)
#+END_SRC

In the "Group Buffer", you can enter a folder by pressing `RET`. But I strongly suggest `C-u RET` because you intend to see all the mails instead of unread mails.

In the folder, you are viewing "Summary Buffer" which is, as I said before, the list of your mails.

Now it's simple. Press `RET` to see the mail. Press `q` to quit "Summary Buffer".

In short, "Group Buffer" lists mail folders. "Summary Buffer" lists mails in one folder.

That's all you need to know. You could go to section [[My Gnus configuration (for Gmail)]] to use Gnus now without reading remaining sections.
** Essential 5%
Don't memorize all the key bindings. Only pick up what you actually use.
*** Search mails
**** Search mails on Server
Press `G G` or `M-x gnus-group-make-nnir-group` to search mails at *server side* in "Group Buffer".

You can press `#` to mark the groups.  Search will be limited to marked groups. `M-#` to unmark.

If no group marked, the group under cursor is searched.

Place the cursor before the first group, all groups will be searched.

To search certain fields in the mail, press `C-u G G` or `C-u M-x gnus-group-make-nnir-group` instead. This is a very useful technique.

You can apply [[http://tools.ietf.org/html/rfc3501#section-6.4.4][more advanced search syntax]] by:
- Press `C-u G G` or `C-u M-x gnus-group-make-nnir-group`
- Input query statements, press Enter
- Type "imap", press enter

**** Filter mails locally
Press `/ /` to limit the mails by subject at "Summary Buffer". "Limiting" means *filtering mails locally*.

Press `/ a` to limit the mails by author at "Summary Buffer".

`/ w` to cancel the current filter.

You can apply the limits sequentially and cancel them in reverse order by pressing `/ w`.

"Limiting" is cool. [[http://www.gnu.org/software/emacs/manual/html_mono/gnus.html#Limiting]] has more tricks.

See [[http://sachachua.com/blog/2008/05/emacs-gnus-searching-mail/]] for technical details.

*** Subscribe groups
Press `A A` or `M-x gnus-group-list-active` in "Group Buffer" to fetch groups list on *all connected server*. It take a while. I suggest pressing "L" to use local cache instead after `A A` once.

After `A A` or `L`, press `u` to subscribe/unsubscribe specific group.

In order to see all the mails in "INBOX" folder/group, you need *MANUALLY* subscribe the group "INBOX"!

Pressing `o` is *much better*. It is the hotkey I created for `C-u 5 M-x gnus-group-list-all-groups`, as mentioned in previous sections.

Press `g` or `M-x gnus-group-get-new-news` to refresh groups list.
*** Reply email
Press `R` to reply with quoted text. Press `r` to reply without quoted text.

Press `S W` (captalized S then captalized W) to reply all with quoted text. It's called "wide reply" in Emacs.

Press `S w` to reply all without quoted text.
*** Compose new email
Press `a` or `m` in "Summary Buffer".

You could also `C-x m` anywhere in Emacs to write a new mail without bugging Gnus.
*** Attach a file
Press `C-c C-a` or `M-x mml-attach-file`.

The attached file is actually plain text embedded in mail body. You can copy and modify the text.
*** Download attachment
Press `o` or `M-x gnus-mime-save-part`. See "[[http://www.gnu.org/software/emacs/manual/html_node/gnus/Using-MIME.html][Using Mime]]" in Emacs manual for details.
*** Send email
Press `C-c C-c` or `M-x message-send-and-exit`.
*** Refresh "Summary Buffer" (check new mails)
hotkey `/ N` or `M-x gnus-summary-insert-new-articles`.
*** Make all mails visible (IMPORTANT)
Press `C-u RET` on the selected group in "Group Buffer", or `C-u M-g` in "Summary Buffer".

That's the *most important part* of this article! By default, Gnus only displays unread mails.

Check [[http://stackoverflow.com/questions/4982831/i-dont-want-to-expire-mail-in-gnus]] for details.
*** Forward mail
Press `C-c C-f` or `M-x gnus-summary-mail-forward` in "Summary Buffer".

You can mark multiple mails to forward (hotkey is "#") and forward them in one mail. [[https://plus.google.com/112423173565156165016/posts][Holger Schauer]] provided the tip.

After the forwarded email is created, you may copy the body of that email without sending it. The copied content could be inserted into new mail.
*** Mark mails as read
Press `c` either in "Summary Buffer" or "Group Buffer". This is *my most frequently used command* because it's easier to use than web Gmail!
** Advanced tips
*** Windows?
It's *100% usable* if you install [[https://www.cygwin.com/][Cygwin]] at first.

Gnus from Cygwin version of Emacs works out of the box.

Native [[https://ftp.gnu.org/gnu/emacs/windows/][Emacs for Windows]] need a little setup:
- Right-click "My Computer" and go to "Properties->Advanced->Environmental Variables"
- Set up *user variables* which does not require Administrator right
- Set the variable "HOME" to the parent directory of your ".emacs.d" directory
- Set the variable "PATH" to the "C:\cygwin\bin". I supposed you install cygwin at C driver
- Install GnuTLS and OpenSSH through Cygwin's package manager
*** Outlook?
Exchange/Outlook users could read my post [[http://blog.binchen.org/posts/how-to-get-email-from-exchange-server-without-outlook.html]].

Basically you need one portable java program [[http://davmail.sourceforge.net/][Davmail]]. That's enough in 99% companies. But if your Adminstrator is evil, you need [[http://ewseditor.codeplex.com/][EWSEditor]] for once.
*** Auto-complete mail address
Install [[http://bbdb.sourceforge.net/][BBDB]]  through [[http://melpa.milkbox.net/#/bbdb][melpa]]. It is a email address database written in Emacs Lisp.

Insert below code into ~/.emacs,
#+BEGIN_SRC emacs-lisp
(add-hook 'message-mode-hook
          '(lambda ()
             (bbdb-initialize 'message)
             (bbdb-initialize 'gnus)
             (local-set-key "<TAB>" 'bbdb-complete-name)))
#+END_SRC

[[https://github.com/company-mode/company-mode][company-mode]] does the similar job which works out of the box.

I use both `M-x bbdb-complete-name` and company-mode is a little picky on BBDB versions.
*** Synchronize from Gmail contacts
Please,
- Go to [[https://www.google.com/contacts/]]
- Click "More -> Export -> vCard Format -> Export".
- Install [[https://github.com/redguardtoo/gmail2bbdb]] (developed by me) and press `M-x gmail2bbdb-import-file`. The contacts will be output into ~/.bbdb which is automatically detected by Emacs

There are alternatives which are picky on BBDB verions. My plugin works always.

*** Customize mail's "From" field
Here is the setup to change "From" field according to the computers I'm using,
#+BEGIN_SRC emacs-lisp
;; (getenv "HOSTNAME") won't work because $HOSTNAME is not an env variable
;; (system-name) won't work because my /etc/hosts has some weird setup in office
(setq my-hostname (with-temp-buffer
        (shell-command "hostname" t)
        (goto-char (point-max))
        (delete-char -1)
        (buffer-string)))

(defun at-office ()
  (interactive)
  (and (string= my-hostname "my-sydney-workpc")
       (not (or (string= my-hostname "homepc")
                (string= my-hostname "eee")))))

(setq user-full-name "My Name"
      user-mail-address (if (at-office) "me@mycompany.com" "me@gmail.com"))
#+END_SRC

Key points:
- Command line program `hostname` is better than Emacs function `(system-name)`
- I works on several computers which does *not* belong to me, so I cannot change /etc/hosts which `(system-name)` try to access
- Please [[http://support.google.com/a/bin/answer.py?hl=en&answer=22370][verify]] your email address at Gmail if you use google's SMTP server
  
*** Classify email
[[http://getpopfile.org/][Popfile]]. Better than Gmail filter.

You may think [[http://www.google.com/inbox/][Google's Inbox]] equals to Popfile. Trust me, it's not. You only need Popfile!

Popfile is open sourced. The data is stored *locally* with my full control. So Popfile still works if I switch the mail server.

For example, after being trained by your Gmail data, Popfile becomes *smarter*. You can use the smarter Popfile to classify mails from your office's Exchange Mail Server.

I'm 100% sure this solution will work because I've been using it for a very long time. I take pity on my colleagues who know only Outlooks.

Check [[http://blog.binchen.org/posts/use-popfile-at-linux.html]] for details.
*** Send HTML mail
    Use [[http://orgmode.org/worg/org-contrib/org-mime.html][org-mime]].
*** Read HTML mail
Install [[http://w3m.sourceforge.net/][w3m]] and [[http://www.emacswiki.org/emacs/emacs-w3m][emacs-w3m]].

Then insert below code into ~/.emacs,
#+BEGIN_SRC emacs-lisp
(setq mm-text-html-renderer 'w3m)
#+END_SRC
*** Read mail offline
Go to the "Summary Buffer".

You need mark the mails by press `!` or `M-x gnus-summary-tick-article-forward`.

The marked mails enter the disk cache. They can be read offline.

You may remove it from the disk cache by `M-x gnus-summary-put-mark-as-read`.

You also need insert below code into ~/.emacs,
#+BEGIN_SRC emacs-lisp
(setq gnus-use-cache t)
#+END_SRC
Above code set `gnus-use-cache` to true to use the cache to the full extent by "wasting" *tens of megabytes* disk space.

The disk cache is located at "~/News/cache/". You can back it up with Github's private repository.
*** Multiple accounts
You only need copy the code containing "gnus-secondary-select-methods" from my setup.

Here is a sample setup,
#+BEGIN_SRC emacs-lisp
(add-to-list 'gnus-secondary-select-methods
             '(nnimap "gmail-second"
                      (nnimap-address "imap.gmail.com")
                      (nnimap-server-port 993)
                      (nnimap-stream ssl)
                      (nnir-search-engine imap)
                      ; @see http://www.gnu.org/software/emacs/manual/html_node/gnus/Expiring-Mail.html
                      ;; press 'E' to expire email
                      (nnmail-expiry-target "nnimap+gmail:[Gmail]/Trash")
                      (nnmail-expiry-wait 90)))
#+END_SRC

`gnus-secondary-select-methods` is the list of your email accounts.

The information of multiple accounts  is stored at  ~/.authinfo.gpg.
*** Why Gnus displays more mails than Gmail
Gnus counts by individual mail. Gmail count by mail thread.
*** Subscribe "[Gmail]/Sent Mail" folder
So Gnus can track *all sent mails*.
*** Reconnect mail server
Press `M-x gnus-group-enter-server-mode` to go into server list.

Move the cursor to "offline" server then press `M-x gnus-server-open-server`.
** My setup
This setup requires Emacs 24.3 at minimum.

Please note the *multiple accounts on the same mail server is troublesome*. Don't waste time on it.

Create ~/.gnus.el,
#+BEGIN_SRC emacs-lisp
(require 'nnir)

;; @see http://www.emacswiki.org/emacs/GnusGmail#toc1
(setq gnus-select-method '(nntp "news.gmane.org")) ;; if you read news groups 

;; ask encyption password once
(setq epa-file-cache-passphrase-for-symmetric-encryption t)

;; @see http://gnus.org/manual/gnus_397.html
(add-to-list 'gnus-secondary-select-methods
             '(nnimap "gmail"
                      (nnimap-address "imap.gmail.com")
                      (nnimap-server-port 993)
                      (nnimap-stream ssl)
                      (nnir-search-engine imap)
                      ; @see http://www.gnu.org/software/emacs/manual/html_node/gnus/Expiring-Mail.html
                      ;; press 'E' to expire email
                      (nnmail-expiry-target "nnimap+gmail:[Gmail]/Trash")
                      (nnmail-expiry-wait 90)))

;; OPIIONAL, the setup for Microsoft Hotmail
(add-to-list 'gnus-secondary-select-methods
             '(nnimap "hotmail"
                      (nnimap-address "imap-mail.outlook.com")
                      (nnimap-server-port 993)
                      (nnimap-stream ssl)
                      (nnir-search-engine imap)
                      (nnmail-expiry-wait 90)))

(setq gnus-thread-sort-functions
      '((not gnus-thread-sort-by-date)
        (not gnus-thread-sort-by-number)))

; NO 'passive
(setq gnus-use-cache t)

;; BBDB: Address list
(add-to-list 'load-path "/where/you/place/bbdb/")
(require 'bbdb)
(bbdb-initialize 'message 'gnus 'sendmail)
(add-hook 'gnus-startup-hook 'bbdb-insinuate-gnus)
(setq bbdb/mail-auto-create-p t
      bbdb/news-auto-create-p t)

;; auto-complete emacs address using bbdb UI
(add-hook 'message-mode-hook
          '(lambda ()
             (flyspell-mode t)
             (local-set-key "<TAB>" 'bbdb-complete-name)))

;; Fetch only part of the article if we can.
;; I saw this in someone's .gnus
(setq gnus-read-active-file 'some)

;; Tree view for groups.  I like the organisational feel this has.
(add-hook 'gnus-group-mode-hook 'gnus-topic-mode)

;; Threads!  I hate reading un-threaded email -- especially mailing
;; lists.  This helps a ton!
(setq gnus-summary-thread-gathering-function 'gnus-gather-threads-by-subject)

;; Also, I prefer to see only the top level message.  If a message has
;; several replies or is part of a thread, only show the first
;; messa  'gnus-thread-ignore-subject' will ignore the subject and
;; look at 'In-Reply-To:' and 'References:' headers.
(setq gnus-thread-hide-subtree t)
(setq gnus-thread-ignore-subject t)

;; Personal Information
(setq user-full-name "My Name"
      user-mail-address "username@gmail.com")

;; Read HTML mail
;; You need install the command line brower 'w3m' and Emacs plugin 'w3m'
(setq mm-text-html-renderer 'w3m)

;; Setup to send email through SMTP
(setq message-send-mail-function 'smtpmail-send-it
      smtpmail-default-smtp-server "smtp.gmail.com"
      smtpmail-smtp-service 587
      smtpmail-local-domain "homepc")
;; http://www.gnu.org/software/emacs/manual/html_node/gnus/_005b9_002e2_005d.html
(setq gnus-use-correct-string-widths nil)
#+END_SRC

Your login and password is stored "~/.authinfo.gpg" which is read by Gnus. `C-h v auth-sources` for more information. 

Multiple mail accounts share one ".authinfo.gpg",
#+BEGIN_SRC conf
machine imap.gmail.com login username@gmail.com password my-secret-password port 993
machine smtp.gmail.com login username@gmail.com password my-secret-password port 587
#+END_SRC

Please note ".authinfo.gpg" is encrypted. Emacs will do the encryption/descryption when reading/writing this file. See [[http://emacswiki.org/emacs/EasyPG]] for details.
